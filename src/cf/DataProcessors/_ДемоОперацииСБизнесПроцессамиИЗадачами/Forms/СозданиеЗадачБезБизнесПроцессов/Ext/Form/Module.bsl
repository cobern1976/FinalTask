
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТемаЗадачи = НСтр("ru = 'Новая задача'");
	СрокИсполнения = ТекущаяДатаСеанса();
	Задача = Задачи.ЗадачаИсполнителя.ПустаяСсылка();
	УстановитьТипыОбъектовАдресации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьСостояниеЭлементов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РольПриИзменении(Элемент)
	
	ОсновнойОбъектАдресации = Неопределено;
	ДополнительныйОбъектАдресации = Неопределено;
	УстановитьТипыОбъектовАдресации();
	УстановитьСостояниеЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьЗадачуВыполнить(Команда)
	
	СоздатьЗадачу();
	ОтобразитьИзменениеДанных(ЗадачаСсылка, ВидИзмененияДанных.Добавление);
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТипыОбъектовАдресации()
	
	ТипыОсновногоОбъектаАдресации = Роль.ТипыОсновногоОбъектаАдресации.ТипЗначения;
	ТипыДополнительногоОбъектаАдресации = Роль.ТипыДополнительногоОбъектаАдресации.ТипЗначения;
	ИспользуетсяСОбъектамиАдресации = Роль.ИспользуетсяСОбъектамиАдресации;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояниеЭлементов()

	ЗаданаРоль = НЕ Роль.Пустая();
	ЗаданыТипыОсновногоОбъектаАдресации = ЗаданаРоль И ИспользуетсяСОбъектамиАдресации
		И ЗначениеЗаполнено(ТипыОсновногоОбъектаАдресации);
	ЗаданыТипыДопОбъектаАдресации = ЗаданаРоль И ИспользуетсяСОбъектамиАдресации 
		И ЗначениеЗаполнено(ТипыДополнительногоОбъектаАдресации);
	Элементы.ОсновнойОбъектАдресации.Доступность = ЗаданыТипыОсновногоОбъектаАдресации;
	Элементы.ДополнительныйОбъектАдресации.Доступность = ЗаданыТипыДопОбъектаАдресации;
	
	Элементы.ОсновнойОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыОсновногоОбъектаАдресации;
	Элементы.ДополнительныйОбъектАдресации.АвтоОтметкаНезаполненного = ЗаданыТипыДопОбъектаАдресации;
	Элементы.ОсновнойОбъектАдресации.ОграничениеТипа = ТипыОсновногоОбъектаАдресации;
	Элементы.ДополнительныйОбъектАдресации.ОграничениеТипа = ТипыДополнительногоОбъектаАдресации;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЗадачу()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗадачаОбъект = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
	ЗадачаОбъект.Дата = ТекущаяДатаСеанса();
	ЗадачаОбъект.Автор = Пользователи.ТекущийПользователь();
	ЗадачаОбъект.РольИсполнителя = Роль;
	ЗадачаОбъект.ОсновнойОбъектАдресации = ОсновнойОбъектАдресации;
	ЗадачаОбъект.ДополнительныйОбъектАдресации = ДополнительныйОбъектАдресации;
	ЗадачаОбъект.Описание = НСтр("ru = 'Задача сгенерирована автоматически.'");
	ЗадачаОбъект.Наименование = ТемаЗадачи;
	ЗадачаОбъект.СрокИсполнения = СрокИсполнения;
	ЗадачаОбъект.Записать();
	
	Задача = Строка(ЗадачаОбъект);
	ЗадачаСсылка = ЗадачаОбъект.Ссылка;
	
КонецПроцедуры

#КонецОбласти
