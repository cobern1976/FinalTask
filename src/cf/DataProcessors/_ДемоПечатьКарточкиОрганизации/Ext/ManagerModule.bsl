#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция СформироватьКарточкуОрганизации(СписокОрганизаций, ОбластиОбъектов) Экспорт
	
	СведенияОбОрганизациях = СведенияОбОрганизациях(СписокОрганизаций);
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка._ДемоПечатьКарточкиОрганизации.ПФ_MXL_КарточкаОрганизации");
	Для Каждого Организация Из СведенияОбОрганизациях Цикл
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		МассивОбластейМакета = Новый Массив;
		МассивОбластейМакета.Добавить("ОсновныеСведения");
		Если Организация.КонтактнаяИнформация.Количество() > 0 Тогда
			МассивОбластейМакета.Добавить("КонтактнаяИнформацияЗаголовок");
			МассивОбластейМакета.Добавить("КонтактнаяИнформацияСтрока");
		КонецЕсли;
		Если Организация.ОтветственныеЛица.Количество() > 0 Тогда
			МассивОбластейМакета.Добавить("ОтветственныеЛицаЗаголовок");
			МассивОбластейМакета.Добавить("ОтветственныеЛицаСтрока");
		КонецЕсли;
		
		Для Каждого ИмяОбласти Из МассивОбластейМакета Цикл
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
			Если СтрЗаканчиваетсяНа(ИмяОбласти, "Строка") Тогда
				ИсточникСведений = Новый Массив;
				Если СтрНайти(ИмяОбласти, "ОтветственныеЛица") = 1 Тогда
					ИсточникСведений = Организация.ОтветственныеЛица;
				ИначеЕсли СтрНайти(ИмяОбласти, "КонтактнаяИнформация") = 1 Тогда
					ИсточникСведений = Организация.КонтактнаяИнформация;
				КонецЕсли;
				
				Для Каждого Сведения Из ИсточникСведений Цикл
					ОбластьМакета.Параметры.Заполнить(Сведения);
					ТабличныйДокумент.Вывести(ОбластьМакета);
				КонецЦикла;
			Иначе
				ЗаполнитьЗначенияСвойств(ОбластьМакета.Параметры, Организация.ОсновныеСведения);
				ТабличныйДокумент.Вывести(ОбластьМакета);
			КонецЕсли;
		КонецЦикла;
	
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбластиОбъектов, Организация.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция СведенияОбОрганизациях(СписокОрганизаций)
	
	Результат = Новый Массив;
	
	Для Каждого Организация Из СписокОрганизаций Цикл
		ОписаниеОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "Код, Представление");
		
		ОсновныеСведения = Новый Структура;
		ОсновныеСведения.Вставить("Наименование", ОписаниеОрганизации.Представление);
		ОсновныеСведения.Вставить("Код", ОписаниеОрганизации.Код);
		ОсновныеСведения.Вставить("ДатаФормирования", ТекущаяДатаСеанса());
		
		КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация));
			
		КонтактнаяИнформация.Колонки.Вид.Имя = "ВидКонтактнойИнформации";
		КонтактнаяИнформация.Колонки.Представление.Имя = "ПредставлениеКонтактнойИнформации";
			
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	_ДемоОтветственныеЛица.ФизическоеЛицо.Наименование КАК ФИО
		|ИЗ
		|	РегистрСведений._ДемоОтветственныеЛица КАК _ДемоОтветственныеЛица
		|ГДЕ
		|	_ДемоОтветственныеЛица.Организация = &Организация";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Организация", Организация);
		ОтветственныеЛица = Запрос.Выполнить().Выгрузить();
		
		
		ОписаниеОрганизации = Новый Структура;
		ОписаниеОрганизации.Вставить("Ссылка", Организация);
		ОписаниеОрганизации.Вставить("ОсновныеСведения", ОсновныеСведения);
		ОписаниеОрганизации.Вставить("КонтактнаяИнформация", КонтактнаяИнформация);
		ОписаниеОрганизации.Вставить("ОтветственныеЛица", ОтветственныеЛица);
		
		Результат.Добавить(ОписаниеОрганизации);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли