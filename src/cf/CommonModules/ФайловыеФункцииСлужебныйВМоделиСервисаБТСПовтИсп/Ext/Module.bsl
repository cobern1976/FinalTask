#Область СлужебныйПрограммныйИнтерфейс

Функция СправочникиФайловИОбъектыХранения() Экспорт
	
	СправочникиФайлов = Новый Соответствие();
	ОбъектыХраненияВИнформационнойБазе = Новый Соответствие();
	
	Обработчики = ОбработчикиИнтеграции();
	
	Для Каждого Обработчик Из Обработчики Цикл
		
		МодульОбработчика = ТехнологияСервисаИнтеграцияСБСП.ОбщийМодуль(Обработчик);
		
		СправочникиФайловОбработчика = МодульОбработчика.СправочникиФайлов();
		Для Каждого СправочникФайловОбработчика Из СправочникиФайловОбработчика Цикл
			СправочникиФайлов.Вставить(СправочникФайловОбработчика.ПолноеИмя(), Обработчик);
		КонецЦикла;
		
		ОбъектыХраненияОбработчика = МодульОбработчика.ОбъектыХраненияФайловИнформационнойБазе();
		Для Каждого ОбъектХраненияОбработчика Из ОбъектыХраненияОбработчика Цикл
			ОбъектыХраненияВИнформационнойБазе.Вставить(ОбъектХраненияОбработчика.ПолноеИмя(), Обработчик);
		КонецЦикла;
		
	КонецЦикла;
	
	Кэш = Новый Структура("СправочникиФайлов, ОбъектыХранения", СправочникиФайлов, ОбъектыХраненияВИнформационнойБазе);
	
	Возврат Кэш;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбработчикиИнтеграции()
	
	ОбработчикиИнтеграции = Новый Массив();
	
	ОбработчикиСобытия = ОбщегоНазначения.ОбработчикиСлужебногоСобытия(
		"ТехнологияСервиса.РаботаВМоделиСервиса.ФайловыеФункцииВМоделиСервиса\ПриЗаполненииОбработчиковИнтеграцииФайловыхФункцийВМоделиСервиса");
	Для Каждого ОбработчикСобытия Из ОбработчикиСобытия Цикл
		ОбработчикСобытия.Модуль.ПриЗаполненииОбработчиковИнтеграцииФайловыхФункцийВМоделиСервиса(ОбработчикиИнтеграции);
	КонецЦикла;
	
	Возврат ОбработчикиИнтеграции;
	
КонецФункции

#КонецОбласти
